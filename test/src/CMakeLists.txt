file(GLOB src_list RELATIVE ${CMAKE_CURRENT_LIST_DIR} ${CMAKE_CURRENT_LIST_DIR}/*.cpp)
foreach(src IN LISTS src_list)
    cmake_path(GET src STEM file_name)
    string(REPLACE "_" "-" target_name ${file_name})
    add_executable(${target_name})
    target_sources(${target_name} PRIVATE ${src})
    target_link_libraries(${target_name} PUBLIC tool-static code-static core-static parser-static rt-static it-static)  # 链接静态库 (导出所有符号)
    set_target_properties(${target_name} PROPERTIES OUTPUT_NAME "test_${file_name}")
    target_compile_definitions(${target_name} PRIVATE IN_CTEST)
    define_filename(${target_name})
    unset(file_name)
    unset(target_name)
endforeach()
unset(src_list)

file(GLOB src_list RELATIVE ${CMAKE_CURRENT_LIST_DIR} ${CMAKE_CURRENT_LIST_DIR}/*.c)
foreach(src IN LISTS src_list)
    cmake_path(GET src STEM file_name)
    string(REPLACE "_" "-" target_name ${file_name})
    add_executable(${target_name})
    target_sources(${target_name} PRIVATE ${src})
    target_link_libraries(${target_name} PUBLIC tool-static-c)  # 链接静态库 (导出所有符号)
    set_target_properties(${target_name} PROPERTIES OUTPUT_NAME "test_${file_name}")
    target_compile_definitions(${target_name} PRIVATE IN_CTEST)
    define_filename(${target_name})
    unset(file_name)
    unset(target_name)
endforeach()
unset(src_list)

# tool 相关测试
file(MD5 "${CMAKE_CURRENT_LIST_DIR}/../share/md5.txt" TOOL_MD5_ANS)
add_new_test(tool-mem COMMAND "$<TARGET_FILE:tool-mem>")
add_new_test(tool-byte COMMAND "$<TARGET_FILE:tool-byte>")
add_new_test(tool-dlc COMMAND "$<TARGET_FILE:tool-byte>" "$<TARGET_FILE:dlc-lib>")
add_new_test(tool-regex COMMAND "$<TARGET_FILE:tool-regex>")
add_new_test(tool-md5 COMMAND "$<TARGET_FILE:tool-md5>" "${TOOL_MD5_ANS}" "${CMAKE_CURRENT_LIST_DIR}/../share/md5.txt")
add_new_test(tool-utf COMMAND "$<TARGET_FILE:tool-utf>")
add_new_test(tool-logger COMMAND "$<TARGET_FILE:tool-logger>")
add_new_test(tool-exit COMMAND "$<TARGET_FILE:tool-exit>")
add_new_test(tool-hash COMMAND "$<TARGET_FILE:tool-hash>")
set_test_label(tool tool-mem tool-byte tool-dlc tool-regex tool-md5 tool-utf tool-logger tool-exit tool-hash)

add_new_test(tool-c-hash COMMAND "$<TARGET_FILE:tool-c-hash>")
add_new_test(tool-c-stdio COMMAND "$<TARGET_FILE:tool-c-stdio>")
set_test_label(tool-c tool-c-hash tool-c-stdio)

add_new_test(code-create COMMAND "$<TARGET_FILE:code-create>")
set_test_label(code code-create)

add_new_test(core-env-var COMMAND "$<TARGET_FILE:core-env-var>")
add_new_test(core-down-msg COMMAND "$<TARGET_FILE:core-down-msg>")
add_new_test(core-up-msg COMMAND "$<TARGET_FILE:core-up-msg>")
set_test_label(core core-env-var core-down-msg core-up-msg)

add_new_test(parser-reader COMMAND "$<TARGET_FILE:parser-reader>")
add_new_test(parser-lexical COMMAND "$<TARGET_FILE:parser-lexical>")
set_test_label(parser parser-reader parser-lexical)

add_new_test(it-syntactic COMMAND "$<TARGET_FILE:it-syntactic>")
add_new_test(it-init COMMAND "$<TARGET_FILE:it-init>")
set_test_label(it it-syntactic it-init)

add_new_test(run-code COMMAND "$<TARGET_FILE:run-code>")
set_test_label(aFun run-code)